#!/usr/bin/perl

use warnings;
use strict;

use Sys::Syslog qw(:DEFAULT setlogsock :macros);
use Config::General;
use POSIX qw(setsid);
use Net::Jabber;
use IO::Poll;
use IPC::Open2;
use POSIX ":sys_wait_h";
use Errno qw(EINTR);
use Data::Dumper;
use Getopt::Std;
use JSON;
use vars qw/ %opts /;

our $opts;

my $appname = "xmppipe";
my $version = "0.0.1";
my $loglevel = 1;
my $confpath = "/etc/xmppipe.conf";
my %conf;
my $pid_file_path = "/var/run/$appname.pid";
my $pipe_cmd = "false";
my $pipe_cmd_stdin;
my $pipe_cmd_stdout;
my $unpriv_uid;
my $unpriv_gid;
my $reload = 0;
my $debug = 0;
my $xmpp;

sub logger
{
	my ($level, @args) = @_;
	my $message = join("", @args);

	if ($debug == 1) {
		print(STDERR "$appname: $message\n");
	} else {
		syslog($level, $message);
	}
}

sub log_fatal
{
	my $msg = shift;
	logger("crit", $msg);
	exit(1);
}

sub sigterm_handler
{
	logger("notice", "caught signal, exiting");
	$xmpp->Disconnect();
	exit(0);
}

sub sighup_handler
{
	logger("notice", "reloading configuration after signal");
	$reload = 1;
	$! = EINTR;
}

sub load_config
{
	my $config = new Config::General(-ConfigFile => $confpath,
		-LowerCaseNames => 1, -AutoTrue => 1) or
		log_fatal("failed to load config $confpath");
	%conf = $config->getall();
	$pid_file_path = $conf{'pid_file_path'} if (defined($conf{'pid_file_path'}));
	$loglevel = $conf{'log_level'} if (defined($conf{'log_level'}));

	# TODO: test if executable
	$pipe_cmd = $conf{'pipe_cmd'} if (defined($conf{'pipe_cmd'}));

	if (defined($conf{'uid'})) {
		$unpriv_uid = getpwnam($conf{'uid'});
	} else {
		$unpriv_uid = getpwnam($appname);
	}

	if (defined($conf{'gid'})) {
		$unpriv_gid = getgrnam($conf{'gid'});
	} else {
		$unpriv_gid = getgrnam($appname);
	}

	if (!defined($unpriv_uid) || !defined($unpriv_gid)) {
		log_fatal("missing uid or gid");
	}
}

sub xmpp_iq
{
	my $sid = shift;
	my $iq = shift;

	logger("debug", "xmpp_iq: ", $iq->GetXML());

	my $reply = $iq->Reply();
	if ($iq->GetQueryXMLNS() ne "http://jabber.org/protocol/disco#info") {
		logger("debug", "not handling " . $iq->GetQueryXMLNS());
		$xmpp->Send($reply);
		return;
	}

	my $q = $reply->GetQuery();

	my $ident = $q->AddIdentity();
	$ident->SetCategory('gateway');
	$ident->SetType('sms');
	$ident->SetName($reply->GetFrom());

	my $feature = $q->AddFeature();
	$feature->SetVar('http://jabber.org/protocol/disco#info');

	# And maybe, if we can (though not at first, not sure voip.ms can)):
	#   <feature var='urn:xmpp:receipts'/>

	logger("debug", "xmpp_iq: sending: " . $reply->GetXML());

	$xmpp->Send($reply);
}

sub xmpp_presence
{
	my $sid = shift;
	my $presence = shift;

	logger("debug", "got presence update type ". $presence->GetType());

	if ($presence->GetType() eq "subscribe") {
		my $reply = $presence->Reply(type => 'subscribed');
		$xmpp->Send($reply);
	} elsif ($presence->GetType() eq "unsubscribe") {
		my $reply = $presence->Reply(type => 'unsubscribed');
		$xmpp->Send($reply);
	} elsif ($presence->GetType() eq "probe") {
		$xmpp->Send($presence->Reply());
	} else {
		logger("debug", "got presence update" .
				" from=" . $presence->GetFrom() .
				", type=" . $presence->GetType() .
				", show=" . $presence->GetShow() .
				", status=" . $presence->GetStatus());
	}
}

sub xmpp_message
{
	my $sid = shift;
	my $message = shift;

	my $msg = {};
	$msg->{'sid'} = $sid;
	$msg->{'to'} = $message->GetTo();
	$msg->{'from'} = $message->GetFrom();
	$msg->{'type'} = $message->GetType();
	$msg->{'subject'} = $message->GetSubject();
	$msg->{'body'} = $message->GetBody();
	$msg->{'thread'} = $message->GetThread();
	$msg->{'timestamp'} = $message->GetTimeStamp();
	$msg->{'error'} = $message->GetError();
	$msg->{'error_code'} = $message->GetErrorCode();

	my $j = to_json($msg, {utf8 => 1, pretty => 1});
	print($pipe_cmd_stdin "$j\n");
}

sub xmpp_auth
{
	logger("info", "connected and authenticated");
}

sub drop_privs
{
	logger("debug", "dropping privs: uid => $unpriv_uid, gid => $unpriv_gid");
	$) = $unpriv_gid;
	$( = $unpriv_gid;
	$> = $unpriv_uid;
	$< = $unpriv_uid;
}

sub daemonize
{
	umask(022);
	chdir('/') or
		log_fatal("chdir(): $!");
	open(STDIN, q{<}, '/dev/null') or
		log_fatal("can't read /dev/null: $!");
	open(STDOUT, q{>}, '/dev/null') or
		log_fatal("can't write to /dev/null: $!");
	open(STDERR, q{>}, '/dev/null') or
		log_fatal("can't write to /dev/null: $!");

	my $pid = fork();

	log_fatal("fork(): $!") if (!defined($pid));

	exit(0) if ($pid > 0);

	open(my $pid_file, ">", $pid_file_path) or
		log_fatal("failed to create PID file: $pid_file_path: $!");
	print($pid_file "$$\n");
	close($pid_file);
	setsid() or
		log_fatal("setsid(): $!");
	$0 = $appname;
}

sub usage
{
	print("Usage: $appname [-hdv]\n");
	print("\t-d\t\tdebug (print errors to STDERR, stays in foreground)\n");
	print("\t-c <path>\tspecify an alternate config path\n");
	print("\t-v\t\tprint version\n");
	print("\t-h\t\tprints this help\n");
}

sub parse_and_send_msg
{
	my $j = shift;

	if (!defined($j->{'to'})) {
		logger("err", "'to' not defined");
		next;
	}

	if (!defined($j->{'from'})) {
		logger("err", "'from' not defined");
		next;
	}

	# Make sure "from" is from our domain, else the component
	# connection dies.
	my @parts = split(/@/, $j->{'from'});
	if (scalar(@parts) < 2) {
		logger("err", "'from' was missing its domain part");
		next;
	}

	if ($parts[1] ne $conf{'xmpp_component_name'}) {
		logger("err", "'from' had an invalid domain");
		next;
	}

	# TODO: probably want to validate the local part as well

	if (!defined($j->{'body'})) {
		logger("err", "'body' not defined");
		next;
	}

	my $msg = Net::XMPP::Message->new();
	$msg->SetMessage(
		to => $j->{'to'},
		from => $j->{'from'},
		body => $j->{'body'});

	if (!defined($j->{'type'})) {
		$msg->SetType("chat");
	}

	if (defined($j->{'subject'})) {
		$msg->SetSubject($j->{'subject'});
	}

	if (defined($j->{'thread'})) {
		$msg->SetSubject($j->{'thread'});
	}

	$xmpp->Send($msg);
}

sub main
{
	getopts("hdvc:", \%opts) or do {
		usage();
		exit(1);
	};

	if (defined($opts{'h'})) {
		usage();
		exit(0);
	}

	if (defined($opts{'v'})) {
		print("$appname version $version\n");
		exit(0);
	}

	if (defined($opts{'d'})) {
		$debug = 1;
	}

	if (defined($opts{'c'})) {
		$confpath = $opts{'c'};
	}

	load_config();

	if ($debug == 0) {
		daemonize();
		openlog($appname, "ndelay,nofatal,pid", "daemon");
		if ($loglevel == 0) {
			setlogmask(LOG_UPTO(LOG_ERR));
		} elsif ($loglevel == 1) {
			setlogmask(LOG_UPTO(LOG_INFO));
		} else {
			setlogmask(LOG_UPTO(LOG_DEBUG));
		}
	}

	$SIG{TERM} = \&sigterm_handler;
	$SIG{INT} = \&sigterm_handler;
	$SIG{HUP} = \&sighup_handler;

	drop_privs() if ($debug == 0);

	$xmpp = new Net::Jabber::Component();

	$xmpp->SetCallBacks(
		onauth => \&xmpp_auth,
		presence => \&xmpp_presence,
		iq => \&xmpp_iq,
		message => \&xmpp_message);

	logger("notice", "connecting to " . $conf{'xmpp_component_host'} .
		":" . $conf{'xmpp_component_port'});

	$xmpp->Connect(
		hostname => $conf{'xmpp_component_host'},
		componentname => $conf{'xmpp_component_name'},
		port => $conf{'xmpp_component_port'}) or do {
			logger("err", "component connection failed: $!");
			return;
	};

	my @xmpp_res = $xmpp->AuthSend(
		secret => $conf{'xmpp_component_secret'}) or do {
			logger("err", "component auth failed: $!");
			return;
	};

        if ($xmpp_res[0] ne "ok") {
                logger("err", "component auth failed: $xmpp_res[0] - $xmpp_res[1]");
                return;
        }

	# TODO: make that configurable
	umask(0077);

	my $pipe_cmd_pid = open2($pipe_cmd_stdout, $pipe_cmd_stdin,
		$conf{'pipe_cmd'}) or do {
		log_fatal("open2: $!");
	};

	my $poll = IO::Poll->new();
	$poll->mask($pipe_cmd_stdout => POLLIN);

	my $json = JSON->new();

	while (defined($xmpp->Process(0))) {
		while ((my $pid = waitpid(-1, WNOHANG)) > 0) {
			my $st = ($? >> 8);
			logger("debug", "child $pid exited ($st)");
		}

		if ($poll->poll(0.2) == -1) {
			next if $!{EINTR};
			log_fatal("poll: $!");
		}

		for my $h ($poll->handles(POLLIN|POLLHUP|POLLERR|POLLNVAL)) {
			my $buf;
			my $r = sysread($h, $buf, 1024);
			if (!defined($r)) {
				next if ($!{EINTR});
				logger("err", "read failed on pipe_cmd: $!");
				next;
			}
			if ($r == 0) {
				logger("info", "pipe command closed stdout;" .
					" restarting");
				$poll->remove($h);
				close($h);
				close($pipe_cmd_stdin);

				$pipe_cmd_pid = open2($pipe_cmd_stdout,
					$pipe_cmd_stdin,
					$conf{'pipe_cmd'}) or do {
						log_fatal("open2: $!");
				};
				$poll->mask($pipe_cmd_stdout => POLLIN);
				$json = JSON->new();
				next;
			}

			my $j;
			eval {
				# incr_parse returns a JSON object if there
				# is enough bytes in the buffer, or undef.
				# It croaks if there's a JSON parse error.
				$j = $json->incr_parse($buf);
			} or do {
				logger("err", "json decode failed");

				# It may be hard to recover from a corrupted
				# JSON buffer, so just recreate it.
				$json = JSON->new();
				next;
			};
			if (defined($j)) {
				parse_and_send_msg($j);
			}
		}
	}

	log_fatal("XMPP component ended unexpectedly: $!");
}

main();
exit(0);
